@tests =
  @test for_block: ||
    count = 0
    for x in 0..10
      for y in -5..5
        if x == y and x < 3
          assert_eq x, y

          # loop bodies share scope of statement
          count += 1

    assert_eq count, 3

  @test for_break_continue: ||
    count = 0
    for i in 0..100
      if i % 2 == 0
        continue
      else if i == 5
        break
      else
        count += 1
    assert_eq count, 2 # 2 odd numbers less than 5

  @test for_assignment: ||
    do_something = |x| x * x
    result = for i in 0..=5
      # The final expression in the loop body is used as the assignment value
      do_something i
    assert_eq result, 25 

  @test while_block: ||
    count = 0
    while count < 5
      count += 1
      assert not count > 5

  @test while_break_continue: ||
    count = 0
    while true
      count += 1
      if count < 10
        continue
      else
        break
      assert false
    assert_eq count, 10

  @test while_assignment: ||
    do_something = |x| x * x
    i = 0
    result = while i < 5
      i += 1
      # The final expression in the loop body is used as the assignment value
      do_something i
    assert_eq result, 25 

  @test until_block: ||
    i = 5
    until i == 0
      i -= 1
    assert_eq i, 0

  @test until_assignment: ||
    i, count = 5, 0
    result = until i == 0
      i -= 1
      count += 1
    assert_eq result, 5

  @test loop_break: ||
    count = 0
    loop
      count += 1
      if count == 5
        break
    assert_eq count, 5
